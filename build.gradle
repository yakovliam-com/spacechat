plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from(sourceSets.main.resources.srcDirs) {
        include 'plugin.yml'
        expand(project.properties)
    }
}

repositories {
    // spigot
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    // placeholderapi
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    // essentials
    maven { url 'https://repo.essentialsx.net/releases/' }
    // triumph-msg
    maven { url 'https://repo.triumphteam.dev/snapshots/' }
    // acf
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    // localelib, rtag
    maven { url 'https://jitpack.io' }

    // bstats, caffeine, adventure, mysql-driver, hikaricp, jedis, configurate
    mavenCentral()
}

dependencies {
    // Compile
    compileOnly 'org.spigotmc:spigot-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly 'me.clip:placeholderapi:2.11.3'
    compileOnly 'net.essentialsx:EssentialsX:2.19.7'

    //compileOnly 'com.google.code.gson:gson:2.8.0'
    //compileOnly 'com.google.guava:guava:21.0'
    compileOnly 'org.jetbrains:annotations:23.0.0'


    // Implementations
    implementation 'com.saicone.ezlib:ezlib:1.1'
    compileOnly 'org.bstats:bstats-bukkit:3.0.0'

    compileOnly 'com.github.ben-manes.caffeine:caffeine:3.1.6'

    compileOnly 'net.kyori:adventure-api:4.13.1'
    compileOnly 'net.kyori:adventure-platform-bukkit:4.3.0'
    compileOnly 'net.kyori:adventure-text-minimessage:4.13.1'

    compileOnly 'me.mattstudios:triumph-msg-adventure:2.2.4-SNAPSHOT'

    implementation 'co.aikar:acf-bukkit:0.5.1-SNAPSHOT'

    // Only used with reflection
    //compileOnly 'mysql:mysql-connector-java:8.0.30'
    compileOnly 'redis.clients:jedis:4.2.3'
    compileOnly 'com.zaxxer:HikariCP:5.0.1'

    compileOnly 'org.spongepowered:configurate-gson:4.1.2'
    compileOnly 'com.github.PikaMug:LocaleLib:338b52b0dc'

    compileOnly 'com.saicone.rtag:rtag:1.3.0'
    compileOnly 'com.saicone.rtag:rtag-item:1.3.0'

    compileOnly 'com.github.cryptomorin:XSeries:9.3.1'
}

compileJava {
    options.release = 11
    options.encoding = 'UTF-8'
}

jar {
    enabled = false
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    dependsOn (shadowJar)
}

shadowJar {
    archiveClassifier.set('')

    // Only need the resource bundle
    exclude 'co/aikar/**/*'

    relocate 'com.saicone.ezlib', project.group + '.lib.ezlib'
    relocate 'org.bstats', project.group + '.lib.bstats'
    relocate 'com.github.benmanes.caffeine.cache', project.group + '.lib.caffeine'
    relocate 'net.kyori.adventure', project.group + '.lib.adventure'
    relocate 'me.mattstudios.msg', project.group + '.lib.msg'
    relocate 'co.aikar.commands', project.group + '.lib.acf.commands'
    relocate 'co.aikar.locales', project.group + '.lib.acf.locales'
    relocate 'com.mysql', project.group + '.lib.mysql'
    relocate 'redis.clients.jedis', project.group + '.lib.jedis'
    relocate 'com.zaxxer.hikari', project.group + '.lib.hikari'
    relocate 'org.spongepowered.configurate', project.group + '.lib.configurate'
    relocate 'me.pikamug.localelib', project.group + '.lib.localelib'
    relocate 'com.saicone.rtag', project.group + '.lib.rtag'
    relocate 'com.cryptomorin.xseries', project.group + '.lib.xseries'

    minimize()
}
