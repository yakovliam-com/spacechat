import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id "com.github.johnrengelman.shadow" version "5.2.0"
    id "java"
    id 'io.github.slimjar' version '1.3.0'
}

// naming
version = '2.3.1-SNAPSHOT'
group = 'dev.spaceseries'
def packageGroup = group + '.spacechat'

repositories {
    mavenCentral()

    maven { url 'https://repo.yakovliam.com/releases/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.codemc.org/repository/maven-public/' }
    maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
    maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
    maven { url 'https://oss.sonatype.org/content/repositories/central/' }
    maven { url 'https://repo.extendedclip.com/content/repositories/placeholderapi/' }
    maven { url 'https://repo.mattstudios.me/artifactory/public/' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation slimjar("1.2.6")

    // https://mvnrepository.com/artifact/org.jetbrains/annotations
    compileOnly group: 'org.jetbrains', name: 'annotations', version: '16.0.1'

    // spigot
    compileOnly 'org.spigotmc:spigot-api:1.17-R0.1-SNAPSHOT'

    // placeholderAPI
    compileOnly 'me.clip:placeholderapi:2.10.9'

    // bstats
    slim "org.bstats:bstats-bukkit:2.2.1"

    // caffeine
    slim 'com.github.ben-manes.caffeine:caffeine:2.9.2'

    // adventure
    slim("net.kyori:adventure-api:4.9.0-SNAPSHOT") {
        exclude group: 'net.kyori', module: 'adventure-bom'
    }
    slim("net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT") {
        exclude group: 'net.kyori', module: 'adventure-bom'
    }
    slim("net.kyori:adventure-text-minimessage:4.1.0-SNAPSHOT") {
        exclude group: 'net.kyori', module: 'adventure-bom'
    }

    // triumph msg adventure
    slim "me.mattstudios:triumph-msg-adventure:2.2.4-SNAPSHOT"

    // acf
    slim "co.aikar:acf-bukkit:0.5.0-SNAPSHOT"

    // mysql
    // https://mvnrepository.com/artifact/mysql/mysql-connector-java
    slim group: 'mysql', name: 'mysql-connector-java', version: '8.0.25'

    // jedis
    slim 'redis.clients:jedis:3.6.3'

    // google
    compileOnly 'com.google.code.gson:gson:2.8.0'
    compileOnly 'com.google.guava:guava:21.0'

    // configurate
    slim 'org.spongepowered:configurate-gson:4.1.1'

    // hikari
    slim('com.zaxxer:HikariCP:5.0.0') {
        exclude group: 'org.slf4j'
    }

    // nbt api
    slim 'de.tr7zw:item-nbt-api-plugin:2.8.0'

    // nbt api (second)
    slim 'io.github.jglrxavpok.hephaistos:common:2.2.0'

    // locale library
    slim 'com.github.PikaMug:LocaleLib:2.2'
}

artifacts {
    archives shadowJar
}

jar {
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

shadowJar {
    archiveName("SpaceChat-${project.version}.jar")
}

slimJar {
    relocate 'dev.spaceseries.spaceapi', "${packageGroup}.lib.spaceapi"
    relocate 'org.bstats', "${packageGroup}.lib.bstats"
    relocate 'net.kyori', "${packageGroup}.lib.kyori"
    relocate 'com.github.benmanes.caffeine', "${packageGroup}.lib.caffeine"
    relocate 'com.mysql', "${packageGroup}.lib.mysql"
    relocate 'me.mattstudios.msg', "${packageGroup}.lib.msg"
    relocate "co.aikar.commands", "${packageGroup}.lib.acf"
    relocate "redis.clients.jedis", "${packageGroup}.lib.jedis"
    relocate "com.zaxxer.hikari", "${packageGroup}.lib.hikari"
    relocate "org.spongepowered.configurate", "${packageGroup}.lib.configurate"
    relocate "io.leangen.geantyref", "${packageGroup}.lib.geantyref"
    relocate "io.github.slimjar", "${packageGroup}.lib.slimjar"
    relocate "de.tr7zw.changeme.nbtapi", "${packageGroup}.lib.nbtapi"
}

processResources {
    filter ReplaceTokens, tokens: [version: project.version]
}

compileJava {
    options.compilerArgs += ["-parameters"]
    options.fork = true
    options.forkOptions.executable = 'javac'
}